<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AAX_InventLocationDimension_LockLink</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class AAX_InventLocationDimension_LockLink
{
    InventParameters InventParameters;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        InventParameters    = InventParameters::find();
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Performs the task of the class.
    /// </summary>
    public void run()
    {
        #OCCRetryCount

        if (!this.validate())
        {
            return;
        }

        try
        {
            ttsbegin;

            InventParameters = InventParameters::find(true);
            InventParameters.AAX_InventLocationDimensionLink = InventMultiSiteDimensionLink::Locked;
            InventParameters.update();

            ttscommit;
        }
        catch (Exception::Deadlock)
        {
            retry;
        }
        catch (Exception::UpdateConflict)
        {
            if (appl.ttsLevel() == 0)
            {
                if (xSession::currentRetryCount() >= #RetryNum)
                {
                    throw Exception::UpdateConflictNotRecovered;
                }
                else
                {
                    retry;
                }
            }
            else
            {
                throw Exception::UpdateConflict;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    public boolean validate()
    {
        boolean ret = true;

        if (ret && !InventParameters.AAX_allowLockLocationDimensionLink())
        {
            ret = checkFailed("@SYS109418");
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static AAX_InventLocationDimension_LockLink construct()
    {
        return new AAX_InventLocationDimension_LockLink();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        AAX_InventLocationDimension_LockLink AAX_InventLocationDimension_LockLink = AAX_InventLocationDimension_LockLink::construct();

        AAX_InventLocationDimension_LockLink.run();

        if (args)
        {
            Common common = args.record();
            if (FormDataUtil::isFormDataSource(common))
            {
                FormDataSource formDataSource = FormDataUtil::getFormDataSource(common);
                formDataSource.executeQuery();
            }
            if (FormDataUtil::isFormDataSource(common) && InventParameters::find().AAX_InventLocationDimensionLink == InventMultiSiteDimensionLink::Locked)
            {
                MenuFunction inventLocationDimensionUpdateMenu = new MenuFunction(menuitemActionStr(AAX_InventLocationDimensionUpdate), MenuItemType::Action);
                if (inventLocationDimensionUpdateMenu.checkAccessRights())
                {
                    inventLocationDimensionUpdateMenu.run();
                }
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>